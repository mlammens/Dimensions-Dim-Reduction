model{
  for( i in 1:n.samp ) {
    y[i] ~ dnorm( mu[i], isigsq )
    mu[i] <- inprod( X[i,], beta[] ) # inner product of X[i,] and beta[]
  }

  alpha <- 1

  for( j in 1:M ){  # M is adequately large
    xi[j] ~ dnorm( 0, itausq )
    a.p[j] <- alpha / M
  }

  S[1] <- 1
  for (j in 2:K) {
    S[j] ~ dcat( p[1:M] )
  }
  for( j in 1:K ){  # K is the number of regression coefficients
    theta[j] <- xi[ S[j] ]
    gamma[j] ~ dbern( pi )
    beta[j] <- theta[j] * gamma[j]
  }

  p[1:M] ~ ddirch( a.p[] )
  pi ~ dunif (0 ,1)

  # Prior for sigsq
  isigsq ~ dgamma (0.1 ,0.1)
  sigsq <- 1 / isigsq

  # Prior for tausq
  itausq ~ dgamma (0.1 ,0.1)
  tausq <- 1 / itausq
}
