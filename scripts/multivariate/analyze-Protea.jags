model{
  for(i in 1:n.samp) {
    y[i,1:n.dim] ~ dmnorm(mu[i,1:n.dim], phi[1:n.dim,1:n.dim])
    for (j in 1:n.dim) {
      mu[i,j] <- inprod(X[i,],beta[j,])
    }
  }

  for(m in 1:M) {  # M is adequately large
    xi[m] ~ dnorm(0.0,tau)
  }

  for(k in 1:K) {  # K is the number of regression coefficients
    for (j in 1:n.dim) {
      theta[j,k] <- xi[S[j,k]]
      S[j,k] ~ dcat(p[j,1:M])
      gamma[j,k] ~ dbern(pi[j])
      beta[j,k] <- theta[j,k]*gamma[j,k]
    }
  }

  for (j in 1:n.dim) {
    pi[j] ~ dunif (0 ,1)
    p[j,1:M] ~ ddirch(a.p[j,])
    for (m in 1:M) {
      a.p[j,m] <- 1.0/M
    }
  }

  ## prior for phi[]
  ##
  phi[1:n.dim,1:n.dim] ~ dwish(Omega[1:n.dim,1:n.dim], wish.nu)

  ## Prior for tausq
  ##
  tau ~ dgamma(1,1)
}
